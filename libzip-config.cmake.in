@PACKAGE_INIT@

# We need to supply transitive dependencies if this config is for a static library
set(IS_SHARED @BUILD_SHARED_LIBS@)
if (NOT IS_SHARED)
  include(CMakeFindDependencyMacro)
  set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_LIST_DIR}/modules")

  set(LIBZIP_ENABLE_BZIP2 @BZIP2_FOUND@)
  set(LIBZIP_ENABLE_LZMA @LIBLZMA_FOUND@)
  set(LIBZIP_ENABLE_ZSTD @ZSTD_FOUND@)
  set(LIBZIP_ENABLE_GNUTLS @GNUTLS_FOUND@)
  set(LIBZIP_ENABLE_MBEDTLS @MBEDTLS_FOUND@)
  set(LIBZIP_ENABLE_OPENSSL @OPENSSL_FOUND@)

  find_dependency(ZLIB 1.1.2)
  if(LIBZIP_ENABLE_BZIP2)
    find_dependency(BZip2)
  endif()

  if(LIBZIP_ENABLE_LZMA)
    find_dependency(LibLZMA 5.2)
  endif()

  if(LIBZIP_ENABLE_ZSTD)
    find_dependency(zstd 1.3.6)
  endif()

  if(LIBZIP_ENABLE_GNUTLS)
    find_dependency(Nettle 3.0)
    find_dependency(GnuTLS)
  endif()
  if(LIBZIP_ENABLE_MBEDTLS)
    find_dependency(MbedTLS 1.0)
  endif()
  if(LIBZIP_ENABLE_OPENSSL)
    find_dependency(OpenSSL)
  endif()
endif()

# Provide all our library targets to users.
include("${CMAKE_CURRENT_LIST_DIR}/libzip-targets.cmake")

check_required_components(libzip)

